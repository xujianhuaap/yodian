apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply from: 'apk.gradle'
//apply from: 'check.gradle'
apply plugin: 'bugly'
bugly {
    appId = '900004839'    //注册时分配的App ID
    appKey = 'E3iqZTUe7N6FfTto'  //注册时分配的App Key
    execute true
    upload true
}
android {

    sourceSets {
        main.java.srcDirs += 'src/chat/java'
        main.res.srcDirs += 'src/chat/res'
        main.jniLibs.srcDirs += 'src/chat/jniLibs'
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    aaptOptions {
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    signingConfigs {
        release {
            keyAlias 'release'
            keyPassword KEY_RELEASE_PASS
            storeFile file(KEY_STORE_PATH)
            storePassword KEY_STORE_PASS
        }
        debug {
            keyAlias 'release'
            keyPassword KEY_DEBUG_PASS
            storeFile file(KEY_STORE_PATH)
            storePassword KEY_STORE_PASS
        }
    }
    defaultConfig {
        applicationId "maimeng.yodian.app.client.android"
        buildConfigField "String", "REDIRECT_URL", APP_REDIRECT_URL
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "unspecified", CHANNEL_ID: -1, LAUNCHER_ACTIVITY: "DefaultActivity"]
        multiDexEnabled true
        minSdkVersion 16
        targetSdkVersion 22//适配Android6.0之前，这里不要修改
        versionCode 1130
        versionName "1.1.30"
        buildConfigField "String", "WEIXIN_APP_KEY", APP_WEIXIN_APP_KEY
        buildConfigField "String", "WEIXIN_APP_SECRET", APP_WEIXIN_APP_SECRET
        buildConfigField "String", "WEIBO_APP_KEY", APP_WEIBO_APP_KEY
        buildConfigField "String", "QQ_APP_KEY", APP_QQ_APP_KEY
    }
    buildTypes {
        release {
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "API_HOST", APP_HOST

        }
        debug {
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            buildConfigField "String", "API_HOST", APP_HOST_DEV
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
repositories {
    flatDir {
        dirs 'libs' //this way we can find the .aar file in libs folder
    }
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(name: 'sharesdk-release', ext: 'aar')
    compile(name: 'hnet-release', ext: 'aar')
    compile project(':PhotoPicker')
    compile project(':AndroidWheel')
    compile(name: 'ViewPagerIndicator-release', ext: 'aar')
    testCompile 'org.robolectric:robolectric:3.0'
    compile 'com.android.support:multidex:1.0.1'
    /**
     * Android Support Libraries
     */
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'io.realm:realm-android:0.84.1'


    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.tencent.bugly:crashreport:1.2.3.8'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'in.srain.cube:ultra-ptr:1.0.10'
    compile 'com.j256.ormlite:ormlite-android:4.46'//数据库
    compile 'com.makeramen:roundedimageview:2.1.2'
    compile('net.glxn.qrgen:android:2.0')//二维码
    compile('com.umeng:fb:5.4.0') {
        exclude module: 'message', group: 'com.umeng'
    }
}
task cleanUnUseRes() << {
    def lint_results = new XmlSlurper().parse(new File(project.getBuildDir(), "outputs/lint-results.xml"))
    lint_results.issue.each { issus ->
        if ("UnusedResources" == issus.@id.toString()) {
            def location = issus.location
            def column = location.@column
            def line = location.@line
            def file = location.@file
            if (column.toString() == "" && line.toString() == "") {
                println "This is Resouce"
                new File(file.toString()).delete()
            } else {
                println "This is String"
                //f.
            }
        }
    }
    println "UnusedResources is remove end"
}
//cleanUnUseRes.dependsOn lint